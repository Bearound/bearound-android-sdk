plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
    id 'org.jetbrains.dokka' version '1.9.10'
}

android {
    namespace 'io.bearound.sdk'

    compileSdk = 36

    buildFeatures {
        buildConfig = true
    }

    defaultConfig {
        minSdk 21
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        buildConfigField("String", "SDK_GROUP_ID", "\"${findProperty("SDK_GROUP_ID")}\"")
        buildConfigField("String", "SDK_ARTIFACT_ID", "\"${findProperty("SDK_ARTIFACT_ID")}\"")
        buildConfigField("String", "SDK_VERSION", "\"${findProperty("SDK_VERSION")}\"")
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    publishing {
        singleVariant("release") {
            withSourcesJar()
        }
    }
}

// Desabilitado temporariamente para desenvolvimento (problema de memória Metaspace)
// task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
//     archiveClassifier.set('javadoc')
//     from dokkaJavadoc.outputDirectory
// }

// Javadoc jar vazio para desenvolvimento
task javadocJar(type: Jar) {
    archiveClassifier.set('javadoc')
}

afterEvaluate {

    publishing {
        publications {
            release(MavenPublication) {
                groupId = findProperty("SDK_GROUP_ID") as String
                artifactId = findProperty("SDK_ARTIFACT_ID") as String
                version = findProperty("SDK_VERSION") as String

                from components.release
                artifact javadocJar

                pom {
                    name.set("BeAround Android SDK")
                    description.set("Kotlin SDK for Android — secure BLE beacon detection and indoor positioning by BeAround.")
                    url.set("https://github.com/Bearound/bearound-android-sdk")

                    licenses {
                        license {
                            name.set("MIT License")
                            url.set("https://opensource.org/licenses/MIT")
                        }
                    }

                    developers {
                        developer {
                            id.set("bearound")
                            name.set("BeAround Team")
                            email.set("contact@bearound.com")
                        }
                    }

                    scm {
                        connection.set("scm:git:https://github.com/Bearound/bearound-android-sdk.git")
                        developerConnection.set("scm:git:ssh://git@github.com:Bearound/bearound-android-sdk.git")
                        url.set("https://github.com/Bearound/bearound-android-sdk")
                    }
                }
            }
        }

        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/Bearound/bearound-android-sdk")
                credentials {
                    username = System.getenv("GPR_USER")
                    password = System.getenv("GPR_KEY")
                }
            }
        }
    }

    tasks.named("generateMetadataFileForReleasePublication") {
        dependsOn(javadocJar)
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.16.0'
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'com.google.android.material:material:1.12.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'

    api 'org.altbeacon:android-beacon-library:2.21.1'
    implementation 'com.google.android.gms:play-services-ads-identifier:18.2.0'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3"
    implementation 'androidx.lifecycle:lifecycle-process:2.9.1'
}